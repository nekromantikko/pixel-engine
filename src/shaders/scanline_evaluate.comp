#version 450
#define MAX_SPRITES_PER_SCANLINE 64

layout(local_size_x = MAX_SPRITES_PER_SCANLINE, local_size_y = 1, local_size_z = 1) in;

struct Sprite
{
	uint pos;
	uint attributes;
};
layout(set = 0, binding = 5) readonly buffer Sprites
{
    Sprite data[];
} sprites;

struct ScanlineData {
	uint spriteCount;
	uint spriteIndices[MAX_SPRITES_PER_SCANLINE];

	int scrollX;
	int scrollY;
};
layout(set = 0, binding = 6) writeonly buffer Scanlines
{
    ScanlineData data[];
} scanlines;

struct RenderState
{
	int scrollX;
	int scrollY;
};
layout(set = 0, binding = 7) readonly buffer RenderStates
{
	RenderState data[];
} renderStates;

void main() {
	uint scanlineIndex = gl_WorkGroupID.y;
	uint globalSpriteIndex = gl_GlobalInvocationID.x;

	// Initialize shared scanline data
	if (globalSpriteIndex == 0) {
		scanlines.data[scanlineIndex].scrollX = renderStates.data[scanlineIndex].scrollX;
		scanlines.data[scanlineIndex].scrollY = renderStates.data[scanlineIndex].scrollY;
	}

	Sprite sprite = sprites.data[globalSpriteIndex];

	uint y = bitfieldExtract(sprite.pos, 0, 16);
	if (scanlineIndex >= y && scanlineIndex < y + 8) {
		uint scanlineSpriteIndex = atomicAdd(scanlines.data[scanlineIndex].spriteCount, 1);
		if (scanlineSpriteIndex < MAX_SPRITES_PER_SCANLINE) {
			scanlines.data[scanlineIndex].spriteIndices[scanlineSpriteIndex] = globalSpriteIndex;
		}
	}
}