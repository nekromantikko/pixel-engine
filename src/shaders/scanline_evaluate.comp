#version 450
#define MAX_SPRITES_PER_SCANLINE 64

layout(local_size_x = MAX_SPRITES_PER_SCANLINE, local_size_y = 1, local_size_z = 1) in;

struct Sprite
{
	uint pos;
	uint attributes;
};
layout(set = 0, binding = 5) readonly buffer Sprites
{
    Sprite data[];
} sprites;

struct ScanlineData {
	uint spriteCount;
	uint spriteIndices[MAX_SPRITES_PER_SCANLINE];

	int scrollX;
	int scrollY;
};
layout(set = 0, binding = 6) writeonly buffer Scanlines
{
    ScanlineData data[];
} scanlines;

struct RenderState
{
	int scrollX;
	int scrollY;
};
layout(set = 0, binding = 7) readonly buffer RenderStates
{
	RenderState data[];
} renderStates;

shared ScanlineData sharedData;

void main() {
	uint scanlineIndex = gl_WorkGroupID.y;
	uint localSpriteIndex = gl_LocalInvocationID.x;

	// Initialize shared scanline data
	if (localSpriteIndex == 0) {
		sharedData.spriteCount = 0;
		sharedData.scrollX = renderStates.data[scanlineIndex].scrollX;
		sharedData.scrollY = renderStates.data[scanlineIndex].scrollY;
	}
	// Wait for initialization to complete
	barrier();

	uint spriteCount = sprites.data.length();
	for (uint globalSpriteIndex = localSpriteIndex; globalSpriteIndex < spriteCount; globalSpriteIndex += MAX_SPRITES_PER_SCANLINE) {
		Sprite sprite = sprites.data[globalSpriteIndex];

		uint y = bitfieldExtract(sprite.pos, 0, 9);
		if (scanlineIndex >= y && scanlineIndex < y + 8) {
			uint scanlineSpriteIndex = atomicAdd(sharedData.spriteCount, 1);
			if (scanlineSpriteIndex < MAX_SPRITES_PER_SCANLINE) {
				sharedData.spriteIndices[scanlineSpriteIndex] = globalSpriteIndex;
			}
		}
	}
	// Wait for processing to complete
	barrier();
	

	if (localSpriteIndex == 0) {
		scanlines.data[scanlineIndex] = sharedData;
	}
}