#version 450
#define MAX_SPRITES_PER_SCANLINE 64
#define NAMETABLE_SIZE 0x1000
#define NAMETABLE_ATTRIBUTE_OFFSET 0xF00

const ivec2 nametableSizePixels = ivec2(512, 480);

layout(local_size_x = 32, local_size_y = 32, local_size_z = 1) in;
layout(set = 0, binding = 0, rgba8) uniform writeonly image2D outImage;
layout(set = 0, binding = 1, rgba8) uniform readonly image1D palette;
struct ChrTile {
	uint p0[2];
	uint p1[2];
	uint p2[2];
};
struct ChrSheet {
	ChrTile tiles[256];
};
layout(set = 0, binding = 2) readonly buffer CHR
{
    ChrSheet sheet[2];
} chr;
layout(set = 0, binding = 3) readonly buffer PalTable
{
    uint data[]; // Again, groups of 4 colors
} palTable;
layout(set = 0, binding = 4) readonly buffer Nametable
{
    uint data[]; // Again, groups of 4 tiles
} nametable;
struct Sprite
{
	int y;
	int x;
	uint tileId;
	uint attributes;
};
layout(set = 0, binding = 5) readonly buffer Sprites
{
    Sprite data[];
} sprites;
struct ScanlineData {
	uint spriteCount;
	uint spriteIndices[MAX_SPRITES_PER_SCANLINE];

	int scrollX;
	int scrollY;
};
layout(set = 0, binding = 6) readonly buffer Scanlines
{
    ScanlineData data[];
} scanlines;

// sRGB to Linear.
float ToLinear1(float c){return(c<=0.04045)?c/12.92:pow((c+0.055)/1.055,2.4);}
vec3 ToLinear(vec3 c){return vec3(ToLinear1(c.r),ToLinear1(c.g),ToLinear1(c.b));}

int ReadNametableByte(int index, int tileOffset) {
	int coarse = tileOffset / 4; // Offset to 4 byte block
	int fine = tileOffset % 4; // byte index (2bits)
	uint nametableOffset = index * (NAMETABLE_SIZE / 4);
	uint result = nametable.data[nametableOffset + coarse];
	// Mask
	result &= (0xFF << (8 * fine));
	result >>= 8 * fine;
	return int(result);
}

int ReadCHRTile(int chrIndex, int tileIndex, int offset, int flipX, int flipY) {
	if (flipX != 0) {
		offset ^= 7;
	}
	if (flipY != 0) {
		offset ^= 56;
	}

	// No 64bit ints, so tiles split in half
	int halfOffset = offset >= 32 ? offset >> 32 : offset;
	int whichHalf = offset >= 32 ? 1 : 0;

	ChrTile tile = chr.sheet[chrIndex].tiles[tileIndex];

	uint t0 = tile.p0[whichHalf];
	uint t1 = tile.p1[whichHalf];
	uint t2 = tile.p2[whichHalf];

	uint result = (t0 & (1 << halfOffset)) >> halfOffset;
	result |= ((t1 & (1 << halfOffset)) >> halfOffset) << 1;
	result |= ((t2 & (1 << halfOffset)) >> halfOffset) << 2;

	return int(result);
}

int ReadPaletteTable(int tableIndex, int colorIndex) {
	int coarse = tableIndex * 2 + colorIndex / 4;
	int fine = colorIndex % 4;
	uint result = palTable.data[coarse];
	result &= (0xFF << (8 * fine));
	result >>= 8 * fine;
	return int(result);
}

ivec2 Scroll(ScanlineData scanline, ivec2 screenPos, out int nametableIndex) {
	ivec2 scrollPos = screenPos;
	scrollPos += ivec2(scanline.scrollX, scanline.scrollY);
	ivec2 nametableIndex2d = (scrollPos / nametableSizePixels);
	if (sign(scrollPos.x) == -1) {
		nametableIndex2d.x -= 1;
	}
	if (sign(scrollPos.y) == -1) {
		nametableIndex2d.y -= 1;
	}
	nametableIndex = (nametableIndex2d.x + nametableIndex2d.y) % 2;
	scrollPos %= nametableSizePixels;
	return scrollPos;
}

ivec2 RenderBackground(ivec2 screenPos) {
	ScanlineData scanline = scanlines.data[screenPos.y];
	int nametableIndex;
	ivec2 scrollPos = Scroll(scanline, screenPos, nametableIndex);

	ivec2 coarse2d = scrollPos / 8; // position of tile
	int coarse = coarse2d.x + coarse2d.y * 64; // Should be 12 bits (yyyyyyxxxxxx)
	ivec2 fine2d = scrollPos % 8; // position of pixel within tile
	int fine = fine2d.x + fine2d.y * 8; // Should be 6 bits (yyyxxx)

	int tileIndex = ReadNametableByte(nametableIndex, coarse);
	
	int colorIndex = ReadCHRTile(0, int(tileIndex), fine, 0, 0);

	// Don't draw transparent pixels
	if (colorIndex == 0) {
		return ivec2(0, 0);
	}

	// Nametable attributes store palette indices for 4x4 tiles in one byte
	int attributeOffset = NAMETABLE_ATTRIBUTE_OFFSET; // Attribute memory starts after all the tile data
	ivec2 attributeBlockPos2d = coarse2d / 4; // Position of big 4x4 block
	int attributeBlockPos = attributeBlockPos2d.x + attributeBlockPos2d.y * 16; // This should be 8 bits (yyyyxxxx)
	ivec2 attributeBlockTilePos2d = coarse2d % 4; // Position of tile within big 4x4 block
	int attributeBlockTilePos = attributeBlockTilePos2d.x + attributeBlockTilePos2d.y * 4; // This should be 4 bits (yyxx)
	ivec2 attributeSubBlockPos2d = attributeBlockTilePos2d / 2; // Position of 2x2 block within 4x4 block
	int attributeSubBlockPos = attributeSubBlockPos2d.x + attributeSubBlockPos2d.y * 2; // This should be 2 bits
	int palTableIndex = ReadNametableByte(nametableIndex, attributeOffset + attributeBlockPos);
	palTableIndex &= (3 << (2 * attributeSubBlockPos));
	palTableIndex >>= 2 * attributeSubBlockPos;

	return ivec2(colorIndex, palTableIndex);
}

vec3 RenderDebugGrid(ivec2 screenPos, vec3 background) {
	ScanlineData scanline = scanlines.data[screenPos.y];
	int nametableIndex;
	ivec2 scrollPos = Scroll(scanline, screenPos, nametableIndex);

	if (scrollPos.x % 16 == 0 || scrollPos.y % 16 == 0) {
		return mix(background, vec3(0.0, 0.0, 1.0), 0.5);
	}
	else if (scrollPos.x % 8 == 0 || scrollPos.y % 8 == 0) {
		return mix(background, vec3(0.0, 0.0, 1.0), 0.25);
	}
	return background;
}

ivec2 RenderSprites(ivec2 screenPos, ivec2 result) {
	ScanlineData scanline = scanlines.data[screenPos.y];
	uint spriteCount = min(scanline.spriteCount, MAX_SPRITES_PER_SCANLINE);

	uint minSpriteIndex = ~uint(0);

	for (int i = 0; i < spriteCount; i++) {
		uint spriteIndex = scanline.spriteIndices[i];
		Sprite sprite = sprites.data[spriteIndex];
		// Is pixel inside sprite?
		if (screenPos.x >= sprite.x && screenPos.x < sprite.x + 8) {
			int palTableIndex = int(sprite.attributes & 3) + 4;
			int priority = int((sprite.attributes >> 5) & 1);
			int flipX = int((sprite.attributes >> 6) & 1);
			int flipY = int((sprite.attributes >> 7) & 1);

			ivec2 fine2d = screenPos - ivec2(sprite.x, sprite.y);
			int fine = fine2d.x + fine2d.y * 8;
			int colorIndex = ReadCHRTile(1, int(sprite.tileId), fine, flipX, flipY);

			// Don't draw transparent pixels
			if (colorIndex == 0) {
				continue;
			}

			// Sprites aren't guaranteed to be sorted so need to check
			// TODO: Maybe a pass after scanline evaluate to sort them?
			if (minSpriteIndex < spriteIndex) {
				continue;
			}
			
			minSpriteIndex = spriteIndex;

			// Draw sprites on top of bg if priority is 0 or if the bg pixel is not opaque
			if (priority == 0 || result.x == 0) {
				result = ivec2(colorIndex, palTableIndex);
			}
		}
	}

	return result;
}

void main() {
	ivec2 screenPos = ivec2(gl_GlobalInvocationID.xy);

	ivec2 result = RenderBackground(screenPos);
	result = RenderSprites(screenPos, result);

	int colorIndex = result.x;
	int palTableIndex = result.y;
	int paletteIndex = ReadPaletteTable(palTableIndex, colorIndex);
	vec3 outColor = imageLoad(palette, paletteIndex).rgb;
	// outColor = RenderDebugGrid(screenPos, outColor);
	outColor = ToLinear(outColor);
	imageStore(outImage, screenPos, vec4(outColor, 1.0));
}